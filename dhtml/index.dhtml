#!/usr/bin/perl
#
# by Ian Watt
# iwatt@myseneca.ca
# 01-12-2015
#

my $debug = 1;

warn "\n\n\n+++++\n" if $debug;

# clean up the environment
# http://perldoc.perl.org/perlsec.html#Cleaning-Up-Your-Path
delete @ENV{ qw( IFS CDPATH ENV BASH_ENV ) };


# ====== module declarations ====== #
use strict;
use warnings;
use CGI;
use DBI;
use Template;
use Apache::Session::MySQL;
use Digest::SHA qw( sha512_base64 );

if ($debug) {
	use diagnostics;
	use Data::Dumper;
}


# ====== constant declarations ====== #
use constant TEMPLATELIB	=> "/srv/Project/wip/tt2";
use constant TEMPLATE		=> "pageMain.tt2";
use constant SELFURL		=> "/wip/session2.dhtml";
use constant RANDOMSALT		=> "58b4a401-3b7a-4c92-aaaf-8273c74752ed";

# ====== global variable declarations ====== #
my (
	$dbObject,
	$cgiObject,
	%session,
	$session_cookie,
	%input,
	%ttConfig,
	$ttObject,
	$ttParameters,
	$cgiHeader,
	$pageView,
);


# ====== begin script execution ====== #

BEGIN {  }

# create database connection object
$dbObject = DBI->connect("DBI:mysql:db_project;localhost;3306",'username','password')
	or die $DBI::errstr;

# create cgi object
$cgiObject = CGI->new();

# get session cookie if there is one.
$session_cookie = $cgiObject->cookie('session');

# tie the session to session hash from cookie
eval {
	tie %session, 'Apache::Session::MySQL', $session_cookie,
		{ Handle => $dbObject , LockHandle => $dbObject };
};
# if the session does not exist then create a new session.
# http://www.perlmonks.org/bare/index.pl?node_id=148811
if ($@ =~ m#^Object does not exist in the data store#) {
	tie %session, 'Apache::Session::MySQL', undef,
		{ Handle => $dbObject , LockHandle => $dbObject };
}

# get cgi form data input if there is any.
%input = $cgiObject->Vars();

warn Data::Dumper->Dump( [ \%session, \%input ], [ qw(*session *input) ] ) if $debug;


# was the login button clicked?
if ( $input{login} ) {
	# was a username and a password provided?
	if ( $input{username} and $input{password} ) {
		# is the username and password valid?
		if ( ValidateLogin( $input{username}, $input{password} ) ) {
			
			warn "[+] Login is valid" if $debug;
			
			# if this is the first visit,
			# store first visit and last visit
			unless ( exists $session{first} ) {
				$session{first} = $session{last} = time;
			}
			# otherwise store only last visit
			else {
				$session{last} = time;
			}
			# record the users ip and user agent
			$session{ip} = $cgiObject->remote_host();
			$session{agent} = $cgiObject->user_agent();
			
			# set access enabled flags
			$session{username} = $input{username};
			$session{access} = $input{username};
			
			# set page to show user
			$pageView = 'access';
			
		}
		# login is invalid
		else {
			warn "[-] Login is invalid" if $debug;
			$pageView = 'login';
		}
	}
}
# was the signup button clicked?
elsif ( $input{signup} ) {
	# was a username, a password, and a confirm provided?
	if ( $input{username} and $input{password} and $input{confirm} ) {
		# is the signup valid?
		if ( ValidateSignup( $input{username}, $input{password}, $input{confirm} ) ) {
			
			warn "[+] Signup is valid" if $debug;
			$pageView = 'login';
			
		}
		# signup is invalid
		else {
			warn "[-] Signup is invalid" if $debug;
			$pageView = 'signup';
		}
	}
}
# was the logout button clicked?
elsif ( $input{logout} ) {
	warn "[!] logout event" if $debug;
	delete $session{access};
	tied(%session)->delete();
	untie(%session);
	$session_cookie = $cgiObject->cookie( -name		=> 'session',
										  -value	=> '',
										  -expires	=> '-1y',
										);
	tie %session, 'Apache::Session::MySQL', undef,
		{ Handle => $dbObject , LockHandle => $dbObject };
	$pageView = 'login';
}
# no input
else {
	warn "[!] no input";
	if ( defined $session_cookie and defined $session{access} and defined $session{last}
			and ( $session_cookie eq $session{_session_id} )
			and ( $session{last} > ( time - 600 ) ) ) {
		
		warn "[@] session has not timed out." if $debug;
		$session{last} = time;
		$session{ip} = $cgiObject->remote_host();
		$session{agent} = $cgiObject->user_agent();
		$session{access} = $session{username};
		$pageView = 'access';
	}
	else {
		warn "[@] session has timed out." if $debug;
		$pageView = 'login';
	}
}

# create session cookie
$session_cookie = $cgiObject->cookie( -name		=> 'session',
									  -value	=> $session{_session_id},
									  -expires	=> '+1m',
									);

# create cgi header
$cgiHeader = $cgiObject->header( -cookie => $session_cookie );

# create template toolkit configuration
%ttConfig = (
	INCLUDE_PATH	=> [ TEMPLATELIB ],
	ENCODING		=> 'utf8',
	PRE_CHOMP		=> 1,
	POST_CHOMP		=> 1,
);

# create template toolkit object
$ttObject = Template->new( \%ttConfig );

# create template toolkit page parameters
$ttParameters = {
	'page'	=> {
		'self'	=>	[ SELFURL ],
		'title'	=>	"INT620",
		'view'	=>	$pageView,
	},
	'session' => \%session,
};
	
# output the cgi header
print $cgiHeader;

# process the template
$ttObject->process( TEMPLATE, $ttParameters )
	or die $ttObject->error();

# disconnect the database object
$dbObject->disconnect();

# done
return;


# retrieves data from the database
# requires a hash containing a sql query string and an array of values
# returns a single database record
sub GetData {
	warn "[!] sub GetData" if $debug;

	my $sql = shift;
	
	my $sqlObject = $dbObject->prepare( $sql->{query} )
		or die $dbObject->errstr;
	
	$sqlObject->execute( @{ $sql->{values} } )
		or die $sqlObject->errstr;
		
	return ( $sqlObject->fetchrow_array );
}

# inserts data into the database
# requires a hash containing a sql query string and an array of values
# returns the id of the inserted object
sub PutData {
	warn "[!] sub PutData" if $debug;

	my $sql = shift;
	
	my $sqlObject = $dbObject->prepare( $sql->{query} )
		or die $dbObject->errstr;
	
	$sqlObject->execute( @{ $sql->{values} } )
		or die $sqlObject->errstr;
		
	return ( $sqlObject->{mysql_insertid} );
}

# validates the user provided username and password
# requires a username string and a password string
# returns userid if login is valid or returns undef
sub ValidateLogin($$) {
	my ($username, $password) = @_;
	my (%sql, $sha512, $result);
	
	# untaint the username and password
	$username = $1 if ( $username =~ /^([\w\-\.]{4,45})$/a );
	$password = $1 if ( $password =~ /^([ -~]{4,45})$/a );
	
	# hash and salt the password and then destroy the password
	# to prevent me from trying to reference it again.
	$sha512 = sha512_base64( $password, RANDOMSALT.$username )
		and undef $password;
	
	# create the sql query statement string
	$sql{query} = qq~SELECT id FROM tbl_plebs WHERE pleb = ? AND code = ?~;
	
	# create the sql values array
	@sql{values} = [ $username, $sha512 ];
	
	# call the database and retrieve the result if any
	$result = GetData( \%sql );
	
	# return the result
	return $result;
}

# validates the user provided username, password and confirmed password
# requires a username string, a password string, and a confirm password string
# returns userid if signup is valid or returns undef
sub ValidateSignup($$$) {
	my ($username, $password, $confirm) = @_;
	my (%sql, $sha512, $result);
	
	# untaint the username, password, and confirmed password
	$username = $1 if ( $username =~ /^([\w\-\.]{4,45})$/a );
	$password = $1 if ( $password =~ /^([ -~]{4,45})$/a );
	$confirm = $1 if ( $confirm =~ /^([ -~]{4,45})$/a );
	
	# if password and confirm match
	if ( $password eq $confirm ) {
		
		# create the sql query statement string
		$sql{query} = qq~SELECT id FROM tbl_plebs WHERE pleb = ?~;
		
		# create the sql values array
		@sql{values} = [ $username ];
		
		# call the database and check if username is not taken
		if ( not GetData( \%sql ) ) {
			
			# check if password is complex
			###TODO: this doesn't check anything but makes testing easier ###
			if ( $password =~ /^(.*)$/ ) {
			
				# hash and salt the password and then destroy the password and
				# confirmed password to prevent me from trying to reference them again.
				$sha512 = sha512_base64( $password, RANDOMSALT.$username )
					and undef $password, $confirm;
				
				# create the sql query statement string
				$sql{query} = qq~INSERT INTO tbl_plebs (pleb,code) VALUES (?,?)~;
				
				# create the sql values array
				@sql{values} = [ $username, $sha512 ];
				
				# call the database to insert data and return result if any
				$result = PutData( \%sql );
			}
			else {
				# the password is too simple
				warn "[-] Password is still possible to remember" if $debug;
			}
		}
		else {
			# the username is already in use
			warn "[-] all the usernames are already taken" if $debug;
		}
	} else {
		# the passwords are different
		warn "[-] your password sucks" if $debug;
	}
	
	# return the result
	return $result;
}

__END__